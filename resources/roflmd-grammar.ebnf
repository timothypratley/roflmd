MARKDOWN = ( ELEMENT | TEXT )*

(* recognized markdown bits *)

ELEMENT = EMBED | PAGE |
          BOLD | ITALIC | STRIKE_THROUGH | HIGHLIGHT | LATEX |
          LINK | IMAGE | ATTRIBUTE |
          INLINE_CODE | CODE_BLOCK | BLOCK_REF | TAG | MULTI_TAG

(* recursive elements *)

EMBED = '{{' MARKDOWN '}}'
PAGE = '[[' MARKDOWN ']]'

(* non-nestable recursive elements *)

BOLD = '**' MARKDOWN '**'
ITALIC = '__' MARKDOWN '__'
STRIKE_THROUGH = '--' MARKDOWN '--'
HIGHLIGHT = '^^' MARKDOWN '^^'
LATEX = '$$' MARKDOWN '$$'

(* non-recursive elements *)

LINK = '[' TEXT '](' TEXT ')'
IMAGE = '![' TEXT '](' TEXT ')'
ATTRIBUTE = NON_WHITESPACE '::' NON_WHITESPACE
INLINE_CODE = '`' TEXT '`'
CODE_BLOCK = '```' TEXT '```'
BLOCK_REF = '((' NON_WHITESPACE '))'
TAG = '#' NON_WHITESPACE
MULTI_TAG = '#[[' TEXT ']]'

(* primitives *)

TEXT = #".*"
NON_WHITESPACE = #"\\S*"
OPTIONAL_WHITESPACE = #'\\s*'
